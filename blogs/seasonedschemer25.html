<!DOCTYPE html><html>

<head>
<meta charset="utf-8">
<title>seasonedschemer25</title>

</head>
<body>
<h1 id="toc_0">Scheme修行 (25) lambda、application</h1>

<p>lambdaは手習いのvalueにもありましたが、set!が出てきた関係で<br>
複数の式を書けるようにする必要があります</p>

<div><pre><code class="language-none">(define *lambda
  (lambda (e table)
    (lambda (args)
      (beglis (body-of e)
              (multi-extend (formals-of e)
                            (box-all args)
                            table)))))</code></pre></div>

<p>クロージャの作られ方が手習いの時とすこし違うかな？<br>
手習いの時はこうでした</p>

<div><pre><code class="language-none">(define *lambda
  (lambda (e table)
    (build (quote non-primitive)
           (cons table (cdr e)))))</code></pre></div>

<p>こちらは単純にテーブル・仮引数・関数本体をリストにして記憶してますね</p>

<p>今回は関数（クロージャ）を作って返し、あとでその関数を評価するようにしています<br>
テーブル・仮引数・関数本体はクロージャに記憶されています<br>
狙いはなんでしょうか</p>

<p>*applicationが出てきたらもう一度見てみます</p>

<p>あと、仮引数をboxに入れているのでdefineで作ったものと同じく<br>
後から値を変更することが可能です</p>

<p>複数の式を処理するbeglisです<br>
なんということはありません</p>

<div><pre><code class="language-none">(define beglis
  (lambda (es table)
    (cond ((null? (cdr? es)) (meaning (car es) table))
          (else ((lambda (val)
                   (beglis (cdr es) table))
                 (meaning (car es) table))))))</code></pre></div>

<p>複数の式がある場合、途中の式の値は捨てられ、最後の式の値が返されます<br>
なんか前の式の値が次の式に与えられるような雰囲気の書き方になってますが<br>
捨てられるだけです<br>
valに値を入れるけど実際には使わない、っていうちょっと変な書き方になってます<br>
こういう書き方しかないのかな？</p>

<div><pre><code class="language-none">          (else (let ()
                   (meaning (car es) table)
                   (beglis (cdr es) table)))</code></pre></div>

<p>とかさらに省いて</p>

<div><pre><code class="language-none">          (else
            (meaning (car es) table)
            (beglis (cdr es) table))</code></pre></div>

<p>でもよさそうな気がしますけど</p>

<p>multi-extendはごく普通に書いてあるだけなので省略<br>
lambdaを評価してみます</p>

<div><pre><code class="language-none">&gt; (value &#39;(lambda (x) x))
#&lt;procedure:...hemer/chap20.rkt:254:4&gt;</code></pre></div>

<p>#&lt;procedure:... というのは値が関数（クロージャ）ですよ、と言ってます<br>
流れの確認</p>

<div><pre><code class="language-none">(value &#39;(lambda (x) x))
(the-meaning &#39;(lambda (x) x))
(meaning &#39;(lambda (x) x) lookup-in-global-table)
((expression-to-action &#39;(lambda (x) x)) &#39;(lambda (x) x) lookup-in-global-table)
(*lambda  &#39;(lambda (x) x) lookup-in-global-table)
(lambda (args)
  (beglis &#39;(x)
    (multi-extend &#39;(x) (box-all args) lookup-in-global-table)))</code></pre></div>

<p>lambdaだけあってもしかたがないので*applicationを作ります</p>

<div><pre><code class="language-none">(define *application
  (lambda (e table)
    ((meaning (function-of e) table)
     (evlis (arguments-of e) table))))</code></pre></div>

<p>ややこしいことやってるはずな割には短くて簡潔の見本みたいなコードですね<br>
手習いだとここが相当しそうです</p>

<div><pre><code class="language-none">(define *application
  (lambda (e table)
    (apply (meaning (function-of e) table)
           (evlis (arguments-of e) table))))
           
(define apply
  (lambda (fun vals)
    (cond ((primitive? fun) (apply-primitive (second fun) vals))
          ((non-primitive? fun) (apply-closure (second fun) vals)))))

(define apply-closure
  (lambda (closure vals)
    (meaning (body-of closure)
             (extend-table (new-entry (formals-of closure) vals)
                           (table-of closure)))))</code></pre></div>

<p>*lambdaと組み合わせて見てみると<br>
applyやapply-closureがなくなってしまっていることがわかります<br>
プリミティブとlambdaを区別せずに評価できるようになってるということですね</p>

<p>手習いの時は、lambdaを評価したときの値は<br>
<code>(non-primitive テーブル 仮引数 本体)</code>というリストで、<br>
これがクロージャを表していることになっていました<br>
クロージャの内容も表示することができたので、クロージャの正体をつかみやすかった記憶があります<br>
ただ、これはただのリストですのでそのまま評価することはできず、<br>
apply-closureでリストを分解してあらためて評価してやる必要がありました</p>

<p>今回はクロージャを表すリストでなく、このようなlambdaといっしょにlambdaを評価した時点の<br>
eとかtableとかを記憶している、クロージャそのものを持っています</p>

<div><pre><code class="language-none">(lambda (args)
  (beglis (body-of e)
          (multi-extend (formals-of e)
                        (box-all args)
                        table))</code></pre></div>

<p>そのため、*applicationで引数を直接あたえて評価することができるようになったんですね<br>
結局手習いの時と同じ動きになっています<br>
プリミティブな関数も同様のしくみで実現するようになっているので<br>
*application以下の関数に場合分けの必要もなくなり、今のようにシンプルにできた、と<br>
*lambdaのところで気になった「狙い」はそういうことだったと思われます</p>

<p>evlisも普通っちゃあ普通</p>

<div><pre><code class="language-none">(define evlis
  (lambda (args table)
    (cond ((null? args) (quote ()))
          (else ((lambda (val)
                   (cons val (evlis (cdr args) table)))
                 (meaning (car args) table))))))</code></pre></div>

<p>さっきの*lambdaにもほとんど同じような書き方が出てきましたが<br>
今度はvalは捨てられずに使われています<br>
健全</p>

<p>「ここでの定義がSchemeで常に動作するようにそうしています」という注がついています<br>
ということは</p>

<div><pre><code class="language-none">          (else (let ()
                   (meaning (car es) table)
                   (beglis (cdr es) table)))</code></pre></div>

<p>みたいな書き方を許さないSchemeがあるということかな？</p>

<p>あらためて考えてみると、(lambda () ...)の「...」に複数の式を並べて<br>
書けなくてもlambdaの入れ子を増やしていけば同じ意味の式になるんですね<br>
これもシンタックスシュガーだったのか</p>

<p>さて*lambda と *application が書けましたので関数を評価できるようになりました</p>

<div><pre><code class="language-none">&gt; (value &#39;((lambda (x) x) 1))
1</code></pre></div>

<p>追います</p>

<div><pre><code class="language-none">global-table -&gt; 空

(value &#39;((lambda (x) x) 1))
(the-meaning &#39;((lambda (x) x) 1))
(meaning &#39;((lambda (x) x) 1) lookup-in-global-table)
((expression-to-action &#39;((lambda (x) x) 1)) 
 &#39;((lambda (x) x) 1) lookup-in-global-table)
(*application  &#39;((lambda (x) x) 1) lookup-in-global-table)
((meaning  &#39;(lambda (x) x) lookup-in-global-table)
 (evlis &#39;(1) lookup-in-global-table))
((lambda (args)
   (beglis &#39;(x)
     (multi-extend &#39;(x) (box-all args) lookup-in-global-table)))
 &#39;(1))
(beglis &#39;(x)
     (multi-extend &#39;(x) (box-all &#39;(1)) lookup-in-global-table)))

global-table -&gt; | x | 1 |

(meaning &#39;x lookup-in-global-table)
(*identifier &#39;x lookup-in-global-table)
1</code></pre></div>

<p>defineと組み合わせることもできます</p>

<div><pre><code class="language-none">&gt; (value &#39;(define id (lambda (x) x)))
&gt; (value &#39;(id 2))
2</code></pre></div>

<p>追います</p>

<div><pre><code class="language-none">global-table -&gt; 空

(value &#39;(define id (lambda (x) x)))
(*define &#39;(define id (lambda (x) x)))
(set! global-table (extend &#39;id
                           (box (the-meaning (lambda (x) x)))
                           global-table))

global-table -&gt; | id | (lambda (args) (beglis &#39;(x) ...) |

(value &#39;(id 2))
(meaning &#39;(id 2) lookup-in-global-table)
(*application &#39;(id 2) lookup-in-global-table)
((meaning &#39;id lookup-in-global-table) (evlis &#39;(2) lookup-in-global-table))
((lambda (args)
   (beglis &#39;(x)
     (multi-extend &#39;(x) (box-all args) lookup-in-global-table)))
 &#39;(2))

global-table -&gt; | id | (lambda (args) (beglis &#39;(x) ...) |
                | x  | 2                                |

(meaning &#39;x lookup-in-global-table)
(*identifier &#39;x lookup-in-global-table)
2</code></pre></div>

<p>lambdaが評価できるようになったということはtrueとfalseとifが定義できるように<br>
なったということですね！</p>

<div><pre><code class="language-none">&gt; (value &#39;(define otrue (lambda (x y) x)))
&gt; (value &#39;(define ofalse (lambda (x y) y)))
&gt; (value &#39;(define oif (lambda (cnd thn els) (cnd thn els))))
&gt; (value &#39;(oif otrue 1 2))
1
&gt; (value &#39;(oif ofalse 1 2))
2</code></pre></div>

<p>これでcondは不要です（嘘とも言い切れない</p>


</body>

</html>
