<!DOCTYPE html><html>

<head>
<meta charset="utf-8">
<title>seasonedschemer22</title>

</head>
<body>
<h1 id="toc_0">Scheme修行 (22) ジェネレータじゃないもの</h1>

<blockquote>
<p>さてこれって継続使わなかったらどう書けるんですかね<br>
これは宿題！</p>
</blockquote>

<p>骨組みはこんな感じでしょう</p>

<div><pre><code class="language-none">(define two-in-a-row*?
  (lambda (l)
    (letrec ((T (lambda (l)
                  (cond ((null? l) #f)
                        ((atom? (car l))
                         ...
                         (T (cdr l)))
                        (else
                         (or (T (car l)) (T (cdr l)))))))
             (T l)))))</code></pre></div>

<p>あとは、直前の値さえ覚えておいて比較すればいいはず</p>

<div><pre><code class="language-none">(define two-in-a-row*?
  (lambda (l)
    (let ((prv (quote ())))
      (letrec ((T (lambda (l)
                    (cond ((null? l) #f)
                          ((atom? (car l))
                           (cond ((eq? (car l) prv) #t)
                                 (else
                                  (set! prv (car l))
                                  (T (cdr l)))))
                          (else
                           (or (T (car l)) (T (cdr l))))))))
        (T l)))))</code></pre></div>

<p>これで動きました<br>
継続版とどっちがいいでしょうねえ</p>

<p>難しい？と思ったのは<br>
set!も使わないで書けるんじゃないかという話<br>
イミュータブルとか関数型プログラミングとか言われてるご時世ですから</p>

<p>直前の値を引数に入れて渡してやれば、と思ったんですがcarで探してきた直前の値を<br>
どうやってcdrに渡してあげればいいのやら</p>

<p>関数がふたつの値を返せればいいのかな<br>
第10の戒律は「同時に2つ以上の値を集めるには関数を作るべし」でした<br>
これってけっきょく収集子を作るって話だったと思うんですけど<br>
まあリストを返せばいいか</p>

<div><pre><code class="language-none">(define pair
  (lambda (a b)
    (cons a (cons b (quote ())))))
(define val car)
(define prv cadr)

(define two-in-a-row*?
  (lambda (l)
    (letrec ((T (lambda (p l)
                  (cond ((null? l) (pair #f (quote())))
                        ((atom? (car l))
                         (cond ((eq? (car l) p) (pair #t (quote ())))
                               (else (T (car l) (cdr l)))))
                        (else (let ((vp (T p (car l))))
                                (cond ((val vp) (pair #t (quote ())))
                                      (else (T (prv vp) (cdr l))))))))))
      (val (T (quote ()) l)))))</code></pre></div>

<p>悪くはないんですけどなんかごちゃごちゃしてる感じがしますね<br>
ふたつの値を返すあたり構文しだいかもしれませんが</p>

<p>リストを渡り歩くという樹状の構造と、アトムを先頭から順番に探してくるという線形の構造が<br>
いっしょくたになっているのがいけないように思います<br>
つまり、やっぱりget-firstとget-nextがほしいってことかなあ<br>
イミュータブルな世界だったらどうやって作るんだろう</p>

<p>といったところで、以前「そこまですんのかHaskell」と思ったことがあるのを思い出しました<br>
たしか、リストの中を渡り歩くために今いるところの左側と右側のリストを<br>
常に覚えているみたいな感じ<br>
この関数では戻る必要が無いので右側だけ覚えておけばよさそうです<br>
つまり、先頭のアトムと、先頭のアトムよりも右側のリストを返して<br>
次はその返されたリストを渡してやれば次のアトムが取れると<br>
なんていうか力技ですね</p>

<p>とにかく書いてみましたがあんまりわかりやすくはないです<br>
Haskellのライブラリではもっとスマートに書いてあるんじゃないでしょうか</p>

<div><pre><code class="language-none">(define 1st car)
(define rest cadr)

(define 1st-and-rest
  (lambda (l)
    (cond ((null? l) (pair (quote ()) (quote())))
          ((atom? (car l)) (pair (car l) (cdr l)))
          (else (let ((a1r (1st-and-rest (car l))))
                  (let ((a1 (1st a1r))
                        (ar (rest a1r)))
                    (cond ((null? a1) (1st-and-rest (cdr l)))
                          (else (pair a1 (cons ar (cdr l)))))))))))</code></pre></div>

<p>とりあえず動いている模様ではあります<br>
カバー率？知りません</p>

<p>ということはtwo-in-a-row*?はすぐ書けますね<br>
値の受け取り方にちょっと注意して</p>

<div><pre><code class="language-none">(define two-in-a-row*?
  (lambda (l)
    (letrec ((T (lambda (p l)
                  (let ((1r (1st-and-rest l)))
                    (cond ((null? (1st 1r)) #f)
                          ((eq? (1st 1r) p) #t)
                          (else (T (1st 1r) (rest 1r))))))))
      (T (quote ()) l))))</code></pre></div>

<p>構造を分けることはできましたけどよくなったのかというと微妙な気分<br>
もし仕事で書くとしたらどれで書くかなあ<br>
普通にset!で前回の値を覚えてそうな気がする</p>

<p>ともあれ宿題は終了</p>


</body>

</html>
